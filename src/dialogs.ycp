/**
 * File:	include/power-management/dialogs.ycp
 * Package:	Configuration of power-management
 * Summary:	Dialogs definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

textdomain "power-management";

import "CWM";
import "Label";
import "Popup";
import "PowerManagement";
import "Wizard";


/**
 * Return a modification status
 * @return true if data was modified
 */
boolean Modified() {
    return PowerManagement::Modified();
}

boolean ReallyAbort() {
    return !PowerManagement::Modified() || Popup::ReallyAbort(true);
}

boolean AskAbort () {
    return Popup::ReallyAbort(true);
}

boolean PollAbort() {
    return UI::PollInput() == `abort;
}

include "power-management/widgets.ycp";

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol ReadDialog() {
    y2milestone ("Running read dialog");
    Wizard::RestoreHelp(HELPS["read"]:"");
    // PowerManagement::AbortFunction = PollAbort;
    boolean ret = PowerManagement::Read();
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
symbol WriteDialog() {
    y2milestone ("Running write dialog");
    Wizard::RestoreHelp(HELPS["write"]:"");
    // PowerManagement::AbortFunction = PollAbort;
    boolean ret = PowerManagement::Write();
    return ret ? `next : `abort;
}

/**
 * Run dialog for ACPI buttons behavior configuration
 * @return symbol for wizard sequencer
 */
symbol MainDialog () {
    y2milestone ("Running main dialog");

    term contents = `HBox (`HSpacing (2), `VBox (
	`VStretch (),
	"scheme_selection",
	`VStretch (),
	`HBox (
	    `HStretch (),
	    `HWeight (1, "battery_warning_button"),
	    `HWeight (1, "acpi_settings_button"),
	    `HWeight (1, "enable_suspend_button"),
	    `HStretch ()
	),
	`VStretch ()
    ), `HSpacing (2));

    // dialog caption
    string caption = _("Power Management Settings");
    list<string> widget_names = ["scheme_selection",
	"battery_warning_button",
	"acpi_settings_button",
	"enable_suspend_button"];

    list<map<string,any> > w = CWM::CreateWidgets (widget_names, widgets);
    string help = CWM::MergeHelps (w);
    contents = CWM::PrepareDialog (contents, w);
    Wizard::SetContentsButtons (caption, contents, help,
	Label::BackButton (), Label::FinishButton ());
    Wizard::HideBackButton();
    Wizard::SetAbortButton(`abort, Label::CancelButton () );
    return CWM::Run (w, $[`abort : ReallyAbort ]);
}

/**
 * Run dialog for ACPI buttons behavior configuration
 * @return symbol for wizard sequencer
 */
symbol ACPIButtonsDialog () {
    y2milestone ("Running ACPI buttons dialog");

    if (PowerManagement::apm_acpi != "ACPI")
    {
	// message popup
	Popup::Message (_("Buttons can be configured only for ACPI. Your
system probably does not support ACPI and uses APM."));
	return `back;
    }

    term contents = `HBox (`HStretch (), `VBox (
	`VStretch(),
	"acpi_buttons",
	`VStretch ()
    ), `HStretch ());

    // dialog caption
    string caption = _("Configuration of ACPI Buttons");

    return CWM::ShowAndRun (["acpi_buttons"],
	widgets, contents, caption, Label::BackButton (), Label::OKButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for battery warnings configuration
 * @return symbol for wizard sequencer
 */
symbol BatteryWarningDialog () {
    y2milestone ("Running battery warning dialog");

    term contents = `HBox (`HStretch (), `VBox (
	`VStretch(),
	"battery_warning",
	`VStretch ()
    ), `HStretch ());

    // dialog caption
    string caption = _("Configuration of Battery Capacity Feedback");

    return CWM::ShowAndRun (["battery_warning"],
	widgets, contents, caption, Label::BackButton (), Label::OKButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for schemes configuration
 * @return symbol for wizard sequencer
 */
symbol EnableSuspendDialog () {
    y2milestone ("Running the suspend/standby allowing dialog");

    term contents = `HBox (`HStretch (), `VBox (
	`VStretch(),
	// frame
	`Frame (_("Allow Switching to Power-Saving Mode"), `HBox (
	    `HStretch (),
	    `VBox (
		`VStretch (),
		`Left ("enable_standby"),
		`VStretch (),
		`Left ("enable_suspend"),
		`VStretch ()
	    ),
	    `HStretch ()
	)),
	`VStretch ()
    ), `HStretch ());

    // dialog caption
    string caption = _("Allow Users to Suspend the Computer");

    return CWM::ShowAndRun ([ "enable_standby", "enable_suspend"],
	widgets, contents, caption, Label::BackButton (), Label::OKButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for schemes configuration
 * @return symbol for wizard sequencer
 */
symbol SchemesDialog () {
    y2milestone ("Running schemes management dialog");

    term contents = `HBox (`HSpacing (2), `VBox (
	`VSpacing (2),
	"schemes_list",
	`VSpacing (2)
    ), `HSpacing (2));

    // dialog caption
    string caption = _("Power Management Scheme Setup");

    return CWM::ShowAndRun (["schemes_list"],
	widgets, contents, caption, Label::BackButton (), Label::OKButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for single scheme configuration
 * @return symbol for wizard sequencer
 */
symbol SchemeDialog1 () {
    y2milestone ("Running single scheme management dialog part 1");

    term contents = `HBox (`HSpacing (2), `VBox (
	`VStretch(),
	"scheme_name",
	`VSpacing (1),
	"scheme_descr",
	`VStretch (),
	`HBox (
	    `HStretch (),
	    "cpu",
	    `HStretch ()
	),
	`VStretch ()
    ), `HSpacing (2));

    // dialog caption
    string caption = _("Power Management Scheme Setup");

    return CWM::ShowAndRun (["scheme_name", "scheme_descr", "cpu"],
	widgets, contents, caption, Label::BackButton (), Label::NextButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for single scheme configuration
 * @return symbol for wizard sequencer
 */
symbol SchemeDialog2 () {
    y2milestone ("Running single scheme management dialog part 1");

    term contents = `HBox (`HStretch (), `VBox (
	`VStretch (),
	"hard_disk",
	`VStretch (),
	"cooling_policy",
	`VStretch ()
    ), `HStretch ());

    // dialog caption
    string caption = _("Power Management Scheme Setup");

    return CWM::ShowAndRun (["hard_disk", "cooling_policy"],
	widgets, contents, caption, Label::BackButton (), Label::NextButton (),
	$[`abort : AskAbort ]);
}

/**
 * Run dialog for single scheme configuration
 * @return symbol for wizard sequencer
 */
symbol SchemeDialog3 () {
    y2milestone ("Running single scheme management dialog part 3");

    term contents = `HBox (`HStretch (), `VBox (
	`VStretch (),
	`Left ("allow_screensaver"),
	`VStretch (),
	`Left ("dpms"),
/*	// frame
	`Frame (_("Display Settings"), `HBox (
	    `HSpacing (2),
	    `VBox (
		`VSpacing (1),
		`Left ("allow_screensaver"),
		`Left ("dpms"),
		`VSpacing (1)
	    ),
	    `HSpacing (2)
	)),
	`VStretch (),
	"suspend_timeout",*/
	`VStretch ()
    ), `HStretch ());

    // dialog caption
    string caption = _("Power Management Scheme Setup");

    return CWM::ShowAndRun ([
	    "allow_screensaver",
	    "dpms",
//	    "suspend_timeout"
	],
	widgets, contents, caption, Label::BackButton (), Label::OKButton (),
	$[`abort : AskAbort ]);
}




// pseudo dialogs

symbol FetchSchemes () {
    PowerManagement::current_schemes = PowerManagement::schemes;
    return `next;
}

symbol StoreSchemes () {
    PowerManagement::schemes = PowerManagement::current_schemes;
    PowerManagement::modified = true;
    return `next;
}

symbol StoreScheme () {
    if (PowerManagement::current_scheme_index == -1)
    {
	PowerManagement::current_schemes = add (
	    PowerManagement::current_schemes,
	    PowerManagement::current_scheme);
    }
    else
    {
	PowerManagement::current_schemes[PowerManagement::current_scheme_index]
	    = PowerManagement::current_scheme;
    }
    return `next;
}

/* EOF */
}
