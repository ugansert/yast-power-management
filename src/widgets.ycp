/**
 * File:	include/power-management/complex.ycp
 * Package:	Configuration of power-management
 * Summary:	Dialogs definitions
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

textdomain "power-management";

import "Label";
import "Popup";
import "PowerManagement";
import "String";

include "power-management/helps.ycp";
include "power-management/misc.ycp";


// scheme selection widget

/**
 * Handle function of a widget
 * @param key string widget id
 * @param event map event that will be handled
 * @return symbol for wizard sequencer
 */
define symbol SchemeSelectionHandle (string key, map event) {
    any ev_id = event["ID"]:nil;
    if (ev_id == `schemes_edit)
	return `schemes_edit;
    if (ev_id == `scheme)
    {
	string current = (string)UI::QueryWidget (`id (ev_id), `Value);
	if (current == "")
	{
	    UI::ChangeWidget (`id (`descr), `Value, _("Default system settings"));
	    return nil;
	}
	string descr = "";
	foreach (map s, PowerManagement::schemes, {
	    if (s["_scheme_id"]:"" == current)
		descr = s["SCHEME_DESCRIPTION"]:"";
	});
	descr = PowerManagement::TranslateSchemeDescription (descr);
	if (descr == "")
	{
	    // fallback scheme description, displayed in a rich text
	    // but without HTML tags!!!
	    descr = _("No scheme description available");
	}
	UI::ChangeWidget (`id (`descr), `Value, descr);
    }

    return nil;
}

/**
 * Init function of a widget
 * @param key string widget id
 */
define void SchemeSelectionInit (string key) {
    list items = maplist (map<string,string> s, PowerManagement::schemes, {
	string id = s["_scheme_id"]:"";
	string name = s["SCHEME_NAME"]:id;
	name = PowerManagement::TranslateSchemeName (name);
	return `item (`id (id), name);
    });
    items = add (items, `item (`id (""), _("Default")));

    UI::ReplaceWidget (`scheme_rp,
	`ComboBox (`id (`scheme), `opt (`hstretch, `notify),
	    // combo box
	    _("&Selected Scheme"), items));
    string scheme = PowerManagement::global_settings["SCHEME"]:"";
    UI::ChangeWidget (`id (`scheme), `Value, scheme);
    SchemeSelectionHandle (key, $["ID" : `scheme ]);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void SchemeSelectionStore (string key, map event) {
    PowerManagement::global_settings["SCHEME"]
	= (string)UI::QueryWidget (`id (`scheme), `Value);
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getSchemeSelectionWidget () {
    return $[
	"widget" : `custom,
	// frame
	"custom_widget" : `Frame (_("Energy Saving Schemes"), `VBox (
	    `HBox (
		`HWeight (1, `VBox (
		    `ReplacePoint (`id (`scheme_rp),
			`ComboBox (`id (`scheme), `opt (`hstretch, `notify),
			    // combo box
			    _("&AC Powered"), [])),
		    `RichText (`id (`descr), `opt (`hstretch), "")
		))
	    ),
	    `HBox (
		`HStretch (),
		`PushButton (`id (`schemes_edit),
		    // push button
		    _("Ed&it Schemes")),
		`HStretch ()
	    )
	)),
	"init" : SchemeSelectionInit,
	"handle" : SchemeSelectionHandle,
	"handle_events" : [`schemes_edit, `scheme, `dc_scheme],
	"store" : SchemeSelectionStore,
	"help" : HELPS["scheme_selection"]:"",
    ];
}


// schemes list widget

/**
 * Redraw the table of energy saving schemes
 */
define void SchemesRedraw () {
    list items = maplist (map<string,string> s,
	PowerManagement::current_schemes,
    {
	string scheme_id = s["_scheme_id"]:"";
	string name = s["SCHEME_NAME"]:scheme_id;
	name = PowerManagement::TranslateSchemeName (name);
	string descr = s["SCHEME_DESCRIPTION"]:"";
	descr = PowerManagement::TranslateSchemeDescription (descr);
	if (descr == "")
	{
	    // fallback scheme description, table entre
	    descr = _("No scheme description available");
	}
	return `item (`id (scheme_id), name, descr);
    });
    UI::ChangeWidget (`id (`schemes), `Items, items);
    UI::SetFocus (`schemes);
}

/**
 * Handle function of a widget
 * @param key string widget id
 * @param event map event that will be handled
 * @return symbol for wizard sequencer
 */
define symbol SchemesHandle (string key, map event) {
    any event_id = event["ID"]:nil;
    string selected = (string)UI::QueryWidget (`id (`schemes), `CurrentItem);
    integer index = -1;
    integer found = -1;
    foreach (map s, PowerManagement::current_schemes, ``{
	index = index + 1;
	if (PowerManagement::current_schemes[index,"_scheme_id"]:"" == selected)
	    found = index;
    });
    if (event_id == `add)
    {
	PowerManagement::current_scheme_index = -1;
	PowerManagement::current_scheme
	    = PowerManagement::current_schemes[found]:$[];
	PowerManagement::current_scheme["_scheme_id"] = "";
	PowerManagement::current_scheme["SCHEME_NAME"] = "";
	PowerManagement::new_schemes[""] = selected;
	return `add;
    }
    else if (event_id == `edit)
    {
	if (selected == nil)
	{
	    // popup message
	    Popup::Message (_("No scheme selected."));
	}
	if (selected == "performance" || selected == "powersave")
	{
	  // FIXME update after talking to MD people
	  if (false)
	  {
	    // popup message
	    Popup::Message (_("The selected scheme cannot be modified.
Add a new one instead."));
	    return nil;
	  }
	}
	PowerManagement::current_scheme_index = found;
	PowerManagement::current_scheme
	    = PowerManagement::current_schemes[found]:$[];
	return `edit;
    }
    else if (event_id == `delete)
    {
	if (selected == nil)
	{
	    // popup message
	    Popup::Message (_("No scheme selected."));
	}
	PowerManagement::current_schemes = filter (
	    map<string,string>s,
	    PowerManagement::current_schemes,
	{
	    return s["_scheme_id"]:"" != selected;
	});
	SchemesRedraw ();
	UI::ChangeWidget (`id (`schemes), `CurrentItem,
	    PowerManagement::current_schemes[0, "_scheme_id"]:"");
	if (haskey (PowerManagement::new_schemes, selected))
	    PowerManagement::new_schemes = (map<string,string>)remove (
		PowerManagement::new_schemes, selected);
    }
    selected = (string)UI::QueryWidget (`id (`schemes), `CurrentItem);
    boolean enabled = true;
    if (selected == "performance" || selected == "powersave")
    {
	enabled = false;
    }
    UI::ChangeWidget (`id (`delete), `Enabled, enabled);
    return nil;
}

/**
 * Init function of a widget
 * @param key string widget id
 */
define void SchemesInit (string key) {
    SchemesRedraw ();
    SchemesHandle (key, $[]);
    UI::ChangeWidget (`id (`schemes), `CurrentItem,
	PowerManagement::current_schemes[0, "_scheme_id"]:"");
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getSchemesListWidget () {
    return $[
	"widget" : `custom,
	"custom_widget" : `VBox (
	    `Table (`id (`schemes), `opt (`notify, `immediate), `header (
		// table header
		_("Scheme Name"),
		// table header
		_("Scheme Description")),
		[]),
	    `HBox (
		`PushButton (`id (`add), Label::AddButton ()),
		`PushButton (`id (`edit), Label::EditButton ()),
		`PushButton (`id (`delete), Label::DeleteButton ()),
		`HStretch ()
	    )
	),
	"init" : SchemesInit,
	"handle" : SchemesHandle,
	"help" : HELPS["schemes_list"]:"",
    ];
}

// scheme name widget

string original_scheme_name = "";
string widget_init_scheme_name = "";

/**
 * Init function of a widget
 * @param key string widget id
 */
define void SchemeNameInit (string key) {
    string scheme_name
	= PowerManagement::current_scheme["SCHEME_NAME"]:"";
    if (scheme_name == "")
        scheme_name = PowerManagement::current_scheme["_scheme_id"]:"";
    scheme_name = PowerManagement::TranslateSchemeName (scheme_name);
    original_scheme_name = PowerManagement::current_schemes[
	PowerManagement::current_scheme_index, "SCHEME_NAME"]:"";
    UI::ChangeWidget (`id (key), `Value, scheme_name);
    widget_init_scheme_name = scheme_name;
}

/**
 * Validate function of a widget
 * @param key string widget id
 * @param event map event that caused widget validation
 * @return boolean true if validation succeeded
 */
define boolean SchemeNameValidate (string key, map event) {
    string new_name = (string)UI::QueryWidget (`id (key), `Value);
    if (new_name == "")
    {
	// popup message
	Popup::Message (_("Scheme name must be set."));
	return false;
    }
    if (new_name == original_scheme_name)
	return true;
    list<string> names = maplist (map s, PowerManagement::current_schemes, {
	return s["SCHEME_NAME"]:s["_scheme_key"]:"";
    });
    if (contains (names, new_name))
    {
	// popup message
	Popup::Message (_("The specified scheme name is not unique."));
	return false;
    }
    if (size (new_name) > 32)
    {
	Popup::Message (
	    // pop-up message
	    _("The scheme name must not be longer than 32 characters."));
	return false;
    }
    return true;
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void SchemeNameStore (string key, map event) {
    string new_name = (string)UI::QueryWidget (`id (key), `Value);
    if (new_name != widget_init_scheme_name)
	PowerManagement::current_scheme["SCHEME_NAME"] = new_name;

    if (original_scheme_name == "")
    {
	string scheme_id = new_name;
	// allow only ASCII characters + numbers in the file name
	scheme_id = filterchars (scheme_id, String::CAlnum());
	if (scheme_id == "")
	    scheme_id = "user";
	list<string> ids = maplist (map s, PowerManagement::current_schemes, {
	    return s["_scheme_id"]:"";
	});
	if (contains (ids, scheme_id))
	{
	    integer index = 0;
	    while (contains (ids, sformat ("%1_%2", scheme_id, index)))
		index = index + 1;
	    scheme_id = sformat ("%1_%2", scheme_id, index);
	}
	PowerManagement::current_scheme["_scheme_id"] = scheme_id;
	PowerManagement::new_schemes[scheme_id]
	    = PowerManagement::new_schemes[""]:"";
    }
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getSchemeNameWidget () {
    return $[
	"widget" : `textentry,
	// text entry
	"label" : _("S&cheme Name"),
	"help" : HELPS["scheme_name"]:"",
	"init" : SchemeNameInit,
	"store" : SchemeNameStore,
	"validate_type" : `function,
	"validate_function" : SchemeNameValidate,
    ];
}

// scheme description widget

string widget_init_scheme_descr = "";

/**
 * Init function of a widget
 * @param key string widget id
 */
define void SchemeDescrInit (string key) {
    string descr = (string)
	(PowerManagement::current_scheme["SCHEME_DESCRIPTION"]:"");
    descr = PowerManagement::TranslateSchemeDescription (descr);
    UI::ChangeWidget (`id (key), `Value, descr);
    widget_init_scheme_descr = descr;
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void SchemeDescrStore (string key, map event) {
    string descr = (string)UI::QueryWidget (`id (key), `Value);
    if (widget_init_scheme_descr != descr)
	PowerManagement::current_scheme["SCHEME_DESCRIPTION"] = descr;
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getSchemeDescrWidget () {
    return $[
	"widget" : `textentry,
	// text entry
	"label" : _("Scheme &Description"),
	"help" : HELPS["scheme_descr"]:"",
	"init" : SchemeDescrInit,
	"store" : SchemeDescrStore,
    ];
}

// hard disk settings widget

/**
 * Init function of a widget
 * @param key string widget id
 */
define void HardDiskInit (string key) {
    string value = (string)
	PowerManagement::current_scheme["SATA_ALPM"]:"";
    if (value == "")
	value = "max_performance";
    UI::ChangeWidget (`id (`alpm), `Value, value);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void HardDiskStore (string key, map event) {
    PowerManagement::current_scheme["SATA_ALPM"]
	= (string)UI::QueryWidget (`id (`alpm), `Value);
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getHardDiskWidget () {
    return $[
	"widget" : `custom,
	// frame,
	"custom_widget" : `Frame (_("SATA Power Management"), `HBox (`HStretch (), `VBox (
	    `VStretch (),
	    // combo box
	    `Left (`ComboBox (`id (`alpm), `opt (`hstretch),
		// combo box
		_("Aggressive Link Power Management"),
		[
		    `item (`id ("min_power"),
			// combo box item
			_("Maximum Power Saving")),
		    `item (`id ("medium_power"),
			// combo box item
			_("Medium Power Saving")),
		    `item (`id ("max_performance"),
			// combo box item
			_("Maximum Performance")),
		])),
	    `VStretch ()), `HStretch ()
	)),
	"init" : HardDiskInit,
	"store" : HardDiskStore,
	"help" : HELPS["hard_disk"]:"",
    ];
}

// cpu policy widget

/**
 * Handle function of a widget
 * @param key string widget id
 * @param event map event that will be handled
 * @return symbol for wizard sequencer
 */
define symbol CpuHandle (string key, map event) {
    string governor = (string)UI::QueryWidget (`id (`governor), `Value);
    UI::ChangeWidget (`id (`ondemand_threshold), `Enabled, governor == "ondemand");
    return nil;
}

/**
 * Init function of a widget
 * @param key string widget id
 */
define void CpuInit (string key) {
y2internal ("CS: %1", PowerManagement::current_scheme);
    UI::ChangeWidget (`id (`governor), `Value,
	PowerManagement::current_scheme["CPUFREQ_GOVERNOR"]:"performance");
    UI::ChangeWidget (`id (`sched_saving), `Value,
	PowerManagement::current_scheme["CPUFREQ_SCHED_MC_POWER_SAVINGS"]:"0" == "1");
    UI::ChangeWidget (`id (`ondemand_threshold), `Value,
	tointeger (PowerManagement::current_scheme["CPUFREQ_ONDEMAND_UP_THRESHOLD"]:"0"));
    UI::ChangeWidget (`id (`powersave_bias), `Value,
	tointeger (PowerManagement::current_scheme["CPUFREQ_ONDEMAND_POWERSAVE_BIAS"]:"1000") / 10);
    CpuHandle (key, nil);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void CpuStore (string key, map event) {
    PowerManagement::current_scheme["CPUFREQ_GOVERNOR"] =
	(string)UI::QueryWidget (`id (`governor), `Value);
    PowerManagement::current_scheme["CPUFREQ_SCHED_MC_POWER_SAVINGS"] =
	(boolean)UI::QueryWidget (`id (`sched_saving), `Value) ? "1" : "0";
    PowerManagement::current_scheme["CPUFREQ_ONDEMAND_UP_THRESHOLD"] =
	tostring ((integer)UI::QueryWidget (`id (`ondemand_threshold), `Value));
    PowerManagement::current_scheme["CPUFREQ_ONDEMAND_POWERSAVE_BIAS"] =
	tostring ((integer)UI::QueryWidget (`id (`powersave_bias), `Value) * 10);
}


map<string,any> getCpuWidget () {
    return $[
	"widget" : `custom,
	// frame,
	"custom_widget" : `Frame (_("CPU Power Management"), `HBox (`HStretch (), `VBox (
	    `VStretch (),
	    // combo box
	    `Left (`ComboBox (`id (`governor), `opt (`hstretch, `notify),
		// combo box
		_("CPU Frequency Governor"),
		[
		    `item (`id ("powersave"),
			// combo box item
			_("Maximum Power Saving")),
		    `item (`id ("performance"),
			// combo box item
			_("Maximum Performance")),
		    `item (`id ("ondemand"),
			// combo box item
			_("On Demand")),
		    `item (`id ("userspace"),
			// combo box item
			_("User Space")),
		])),
	    `VStretch (),
	    `Left (`IntField (`id (`ondemand_threshold), _("Load Checking Interval"), 0, 1000000, 0)),
	    `VStretch (),
	    `Left (`IntField (`id (`powersave_bias), _("Lower frequency by (percent)"), 0, 100, 0)),
	    `VStretch (),
	    `Left (`CheckBox (`id (`sched_saving), _("Balance Load between CPU Cores")))
	), `HStretch ())),
	"init" : CpuInit,
	"store" : CpuStore,
	"handle" : CpuHandle,
	"handle_events" : [ `governor ],
	"help" : HELPS["cpu"]:"",
    ];
}

// cpu cooling policy widget

/**
 * Init function of a widget
 * @param key string widget id
 */
define void CoolingPolicyInit (string key) {
    string value = (string)
	PowerManagement::current_scheme["COOLING_POLICY"]:"";
    if (value == "")
	value = "passive";
    UI::ChangeWidget (`id (`cooling_policy), `Value, value);
    UI::ChangeWidget (`id (`overheat), `Value,
	PowerManagement::current_scheme["THERMAL_HOT_ACTION"]:
		"notify");
    UI::ChangeWidget (`id (`overheat_critical), `Value,
	PowerManagement::current_scheme["THERMAL_CRITICAL_ACTION"]:
		"suspend");
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void CoolingPolicyStore (string key, map event) {
    PowerManagement::current_scheme["COOLING_POLICY"]
	= (string)UI::QueryWidget (`id (`cooling_policy), `Value);
    PowerManagement::current_scheme["THERMAL_HOT_ACTION"]
	= (string)UI::QueryWidget (`id (`overheat), `Value);
    PowerManagement::current_scheme["THERMAL_CRITICAL_ACTION"]
	= (string)UI::QueryWidget (`id (`overheat_critical), `Value);
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getCoolingPolicyWidget () {
    return $[
	"widget" : `custom,
	// frame
	"custom_widget" : `Frame (_("Cooling Policy"), `VBox (
	    // combo box
	    `ComboBox (`id (`cooling_policy), `opt (`hstretch), _("&Status"), [
		//combo box item
		`item (`id ("active"), _("Active")),
		// combo box item
		`item (`id ("passive"),_("Passive")),
	    ]),
	    `ComboBox (`id (`overheat),
		`opt (`hstretch),
		// combo box
		_("&Overheat Temperature Action"), []),
//		GetAcpiThermalEventHandlers ()),
	    `ComboBox (`id (`overheat_critical),
		`opt (`hstretch),
		// combo box
		_("&Critical Temperature Action"), [])
//		GetAcpiThermalEventHandlers ())
	)),
	"init" : CoolingPolicyInit,
	"store" : CoolingPolicyStore,
	"help" : HELPS["cooling_policy"]:"",
    ];
}

// allow screen saver widget

/**
 * Init function of a widget
 * @param key string widget id
 */
define void ScreensaverInit (string key) {
    boolean value = tolower ((string)
	PowerManagement::current_scheme["DISABLE_SCREEN_SAVER"]:"")
		== "yes"
	? false
	: true;
    UI::ChangeWidget (`id ("enable_screensaver"), `Value, value);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void ScreensaverStore (string key, map event) {
    PowerManagement::current_scheme["DISABLE_SCREEN_SAVER"]
	= (boolean)UI::QueryWidget (`id ("enable_screensaver"), `Value)
	    ? "no"
	    : "yes";
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getScreensaverWidget () {
    return $[
	"widget" : `custom,
	"custom_widget" : `Frame (
	    // frame
            _("Screen Saver Settings"),
	    `HBox (
		`HSpacing (2),
		`VBox (
		    `VSpacing (1),
                    `Left (`CheckBox (`id ("enable_screensaver"),
                        // check box
			_("Enable &Screen Saver"))),
		    `VSpacing (1)
		),
		`HSpacing (2)
	    )
        ),
	"help" : HELPS["allow_screensaver"]:"",
	"init" : ScreensaverInit,
	"store" : ScreensaverStore,
    ];
}

// DPMS widget

/**
 * Handle function of a widget
 * @param key string widget id
 * @param event map event that will be handled
 * @return symbol for wizard sequencer
 */
define symbol DpmsHandle (string key, map event) {
    boolean enabled = (boolean)UI::QueryWidget (`id (`enable_dpms), `Value);
    UI::ChangeWidget (`id (`dpms_stdby), `Enabled, enabled);
    UI::ChangeWidget (`id (`dpms_susp), `Enabled, enabled);
    UI::ChangeWidget (`id (`dpms_off), `Enabled, enabled);
    return nil;
}

/**
 * Init function of a widget
 * @param key string widget id
 */
define void DpmsInit (string key) {
    boolean value = tolower ((string)
	PowerManagement::current_scheme["DISABLE_DPMS"]:"")
		== "yes"
	? false
	: true;
    UI::ChangeWidget (`id (`enable_dpms), `Value, value);
    UI::ChangeWidget (`id (`dpms_stdby), `Value, tointeger (
	PowerManagement::current_scheme["DPMS_STANDBY"]:"10"));
    UI::ChangeWidget (`id (`dpms_susp), `Value, tointeger (
	PowerManagement::current_scheme["DPMS_SUSPEND"]:"20"));
    UI::ChangeWidget (`id (`dpms_off), `Value, tointeger (
	PowerManagement::current_scheme["DPMS_OFF"]:"30"));
    DpmsHandle (key, $[]);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
define void DpmsStore (string key, map event) {
    PowerManagement::current_scheme["DISABLE_DPMS"]
	= (boolean)UI::QueryWidget (`id (`enable_dpms), `Value)
	    ? "no"
	    : "yes";
    PowerManagement::current_scheme["DPMS_STANDBY"]
	= sformat ("%1", UI::QueryWidget (`id (`dpms_stdby), `Value));
    PowerManagement::current_scheme["DPMS_SUSPEND"]
	= sformat ("%1", UI::QueryWidget (`id (`dpms_susp), `Value));
    PowerManagement::current_scheme["DPMS_OFF"]
	= sformat ("%1", UI::QueryWidget (`id (`dpms_off), `Value));
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
define map<string,any> getDpmsWidget () {
    return $[
	"widget" : `custom,
	"custom_widget" : `Frame (
	    // frame
	    // Display is a substantive (screen)
	    _("Display Power Management Settings"),
	    `HBox (
		`HSpacing (2),
		`VBox (
		    `VSpacing (1),
		    `Left (`CheckBox (`id (`enable_dpms), `opt (`notify),
			// check box
			// Display is a substantive (screen)
			_("Enable &Display Power Management"))),
		    `VSpacing (1),
		    `IntField (`id (`dpms_stdby),
			// int field
			// Display is a substantive (screen)
			_("Display S&tandby Time-Out (minutes)"), 0, 120, 1),
		    `IntField (`id (`dpms_susp),
			// int field
			// Display is a substantive (screen)
			_("Display &Suspend Time-Out (minutes)"), 0, 120, 2),
		    `IntField (`id (`dpms_off),
			// int field
			// Display is a substantive (screen)
			_("Display Power &Off Time-Out (minutes)"), 0, 120, 3),
		    `VSpacing (1)
		),
		`HSpacing (2)
	    )
	),
	"help" : HELPS["dpms"]:"",
	"init" : DpmsInit,
	"store" : DpmsStore,
	"handle" : DpmsHandle,
    ];
}

// suspend timeout widget

/**
 * Handle function of a widget
 * @param key string widget id
 * @param event map event that will be handled
 * @return symbol for wizard sequencer
 */
/*define symbol SuspendTimeoutHandle (string key, map event) {
    boolean enabled = (boolean)
	UI::QueryWidget (`id ("enable_suspend_timeout"), `Value);
    UI::ChangeWidget (`id ("suspend_timeout"), `Enabled, enabled);
    UI::ChangeWidget (`id ("suspend_method"), `Enabled, enabled);
    return nil;
}

/**
 * Init function of a widget
 * @param key string widget id
 */
/*define void SuspendTimeoutInit (string key) {
    UI::ChangeWidget (`id ("suspend_method"), `Value,
	PowerManagement::current_scheme["SUSPEND_METHOD"]:"standby");
    string timeout_str = (string)
	PowerManagement::current_scheme["SUSPEND_TIMEOUT"]:"";
    if (timeout_str == "")
	timeout_str = "1";
    integer timeout = tointeger (timeout_str);
    UI::ChangeWidget (`id ("enable_suspend_timeout"), `Value,
	timeout != 0);
    UI::ChangeWidget (`id ("suspend_timeout"), `Value, timeout);
    SuspendTimeoutHandle (key, $[]);
}

/**
 * Store settings of a widget
 * @param key string widget id
 * @param event map event that caused storing of widget settings
 */
/*define void SuspendTimeoutStore (string key, map event) {
    boolean enabled = (boolean)
	UI::QueryWidget (`id ("enable_suspend_timeout"), `Value);
    PowerManagement::current_scheme["SUSPEND_TIMEOUT"] = enabled
	? sformat ("%1", UI::QueryWidget (`id ("suspend_timeout"), `Value))
	: "0";
    PowerManagement::current_scheme["SUSPEND_METHOD"]
	= (string)UI::QueryWidget (`id ("suspend_method"), `Value);
}

/**
 * Get description map of a widget
 * @return a map widget description map
 */
/*define map<string,any> getSuspendTimeoutWidget () {
    return $[
	"widget" : `custom,
	"custom_widget" : `Frame (_("Syspend Suspend Settings"), `HBox (
	    `HSpacing (2),
	    `VBox (
		`VSpacing (1),
		`Left (`CheckBox (`id ("enable_suspend_timeout"),
		    `opt (`notify),
		    _("Enable System Suspend Timeout"))),
		`IntField (`id ("suspend_timeout"),
		    _("Suspend Timeout (minutes)"), 1, 360, 1),
		`ComboBox (`id ("suspend_method"), `opt (`hstretch),
		    _("Suspend Method"),
		    [
			`item (`id ("standby"), _("Standby")),
			`item (`id ("str"), _("Suspend to RAM")),
			`item (`id ("std"), _("Suspend to disk")),
		    ]),
		`VSpacing (1)
	    ),
	    `HSpacing (2)
	)),
	"help" : HELPS["suspend_timeout"]:"",
	"init" : SuspendTimeoutInit,
	"store" : SuspendTimeoutStore,
	"handle" : SuspendTimeoutHandle,
	"handle_events" : ["enable_suspend_timeout"],
    ];
}
*/
// general settings

map<string,map<string,any> > widgets = $[
    "scheme_selection" : getSchemeSelectionWidget (),
    "schemes_list" : getSchemesListWidget (),
    "scheme_name" : getSchemeNameWidget (),
    "scheme_descr" : getSchemeDescrWidget (),
    "hard_disk" : getHardDiskWidget (),
    "cpu" : getCpuWidget (),

    "cooling_policy" : getCoolingPolicyWidget (),
    "screensaver" : getScreensaverWidget (),
    "dpms" : getDpmsWidget (),
//    "suspend_timeout" : getSuspendTimeoutWidget ()
];

/* EOF */
}
